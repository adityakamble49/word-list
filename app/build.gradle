apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.oss.licenses.plugin'


android {
    compileSdkVersion buildConfig.compileSdk

    defaultConfig {
        applicationId "com.adityakamble49.wordlist"

        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk

        versionCode buildConfig.version.code
        versionName buildConfig.version.name

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        debug {
            buildConfigField 'String', "GitHubApiKey", WordList_GitHubApiKey
            applicationIdSuffix ".debug"
        }
        staging {
            buildConfigField 'String', "GitHubApiKey", WordList_GitHubApiKey
            applicationIdSuffix ".staging"
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            buildConfigField 'String', "GitHubApiKey", WordList_GitHubApiKey
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            all {
                systemProperty 'robolectric.enabledSdks', '21'
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        androidTest.java.srcDirs += "src/test-common/java"
        test.java.srcDirs += "src/test-common/java"
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'kotlin/**'
        exclude 'META-INF/*.kotlin_module'
        exclude 'META-INF/*.version'
    }

    lintOptions {
        quiet true
        abortOnError false
        ignoreWarnings true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation deps.kotlin.stdlib.jre
    implementation deps.android.support.appcompatV7
    implementation deps.android.support.preferenceV7
    implementation deps.android.support.preferenceV14
    implementation deps.android.support.recyclerView
    implementation deps.android.support.constraintLayout
    implementation deps.android.support.design
    implementation deps.materialDialogs.core
    implementation deps.materialAbout.core
    implementation deps.getbaseFAB.core
    implementation deps.timber.core
    implementation deps.gson.core
    implementation deps.rx.rxAndroid
    implementation deps.rx.rxKotlin
    implementation deps.dagger.runtime
    implementation deps.dagger.support
    implementation deps.retrofit.core
    implementation deps.retrofit.converterGson
    implementation deps.retrofit.retrofitAdapter
    implementation deps.okhttp.core
    implementation deps.okhttp.loggingInterceptor
    implementation deps.jsoup.core
    kapt deps.dagger.processor
    kapt deps.dagger.compiler
    implementation deps.android.arch.archRuntime
    implementation deps.android.arch.archExtensions
    implementation deps.android.arch.roomRuntime
    kapt deps.android.arch.archCompiler
    kapt deps.android.arch.roomCompiler
    implementation deps.google.oosLicenses.core

    debugImplementation deps.debugDb.core

    testImplementation deps.test.junit
    testImplementation deps.test.mockito
    testImplementation deps.test.robolectric
    androidTestImplementation deps.test.mockitoAndroid
    androidTestImplementation deps.test.supportRunner
    androidTestImplementation deps.test.expressoCore
}
